plugins {
    id("java-library")
    id("maven-publish")

    // gRPC
    id("com.google.protobuf") version "0.8.18"
}

group "dev.emortal.api.kurushimi"
version "1.0"

def grpcVersion = "1.50.2"
def protobufVersion = "3.21.9"
def protocVersion = protobufVersion

repositories {
    mavenCentral()
    maven { url = "https://repo.emortal.dev/snapshots" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    protobuf("com.google.protobuf:protobuf-java:${protobufVersion}")
    api("com.google.protobuf:protobuf-java:3.21.8")
    api("org.apache.tomcat:annotations-api:6.0.53")

    compileOnly("dev.emortal.api:common-proto-sdk:0f7f5c0") // Used for Functional gRPC responses
    compileOnly("com.github.Minestom.Minestom:Minestom:84846f663b") // Used for moving players to lobby

    api("io.grpc:grpc-netty:${grpcVersion}")
    api("io.grpc:grpc-stub:${grpcVersion}")
    api("io.grpc:grpc-protobuf:${grpcVersion}")

    compileOnly("org.jetbrains:annotations:23.1.0")
    annotationProcessor("org.jetbrains:annotations:23.1.0")

    implementation("org.slf4j:slf4j-api:2.0.6")
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

publishing {

    repositories {
        maven {
            name = "development"
            url = uri("https://repo.emortal.dev/snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
        }
        maven {
            name = "release"
            url = uri("https://repo.emortal.dev/releases")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = "dev.emortal.api"
            artifactId = "kurushimi-sdk"

            def commitHash = System.getenv("COMMIT_HASH_SHORT")
            def releaseVersion = System.getenv("RELEASE_VERSION")
            version = commitHash ?: releaseVersion ?: "local"

            from components.java
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs "build/generated/source/proto/main/grpc"
            srcDirs "build/generated/source/proto/main/java"
        }

        proto {
            srcDir "src/proto"
        }
    }
}
